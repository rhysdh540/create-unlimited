apply from: rootProject.file('gradle/platform.gradle')

architectury.forge()

configurations {
    developmentForge.extendsFrom common
	forgeRuntimeLibrary.extendsFrom addJar
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.minecraft_version}-${rootProject.forge}"

	modImplementation("com.simibubi.create:create-${minecraft_version}:${create_forge}:slim") { transitive = false }
	modImplementation "com.tterrag.registrate:Registrate:${registrate}"
	modImplementation "com.jozufozu.flywheel:flywheel-forge-${flywheel_mc}:${flywheel}"

	include "com.github.LlamaLad7:MixinExtras:${rootProject.mixin_extras}"

	if(rootProject.hasProperty('snr')) {
		modImplementation("com.railwayteam.railways:Steam_Rails-forge-${minecraft_version}:${snr}+forge-mc${minecraft_version}-build.${snr_build}")
	}
}

loom {
	def awFile = project(':common').file('src/main/resources/createunlimited.accesswidener')

	forge {
		mixinConfig(
			'createunlimited.mixins.json',
			'createunlimited-common.mixins.json',
		)

		if(awFile.exists()) {
			convertAccessWideners = true
			extraAccessWideners.add loom.accessWidenerPath.get().asFile.name
		}
	}
}

shadowJar {
    exclude 'fabric.mod.json'
}
