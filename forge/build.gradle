plugins {
	id "com.github.johnrengelman.shadow" version "${shadow_version}"
}

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common

	forgeRuntimeLibrary.extendsFrom addJar
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.minecraft_version}-${rootProject.forge}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }

	modImplementation("com.simibubi.create:create-${minecraft_version}:${create_forge}:slim") { transitive = false }
	modImplementation "com.tterrag.registrate:Registrate:${registrate}"
	modImplementation "com.jozufozu.flywheel:flywheel-forge-${flywheel_mc}:${flywheel}"

	include "com.github.LlamaLad7:MixinExtras:${rootProject.mixin_extras}"

	if(rootProject.hasProperty("snr")) {
		modImplementation("com.railwayteam.railways:Steam_Rails-forge-${minecraft_version}:${snr}+forge-mc${minecraft_version}-build.${snr_build}")
	}
}

loom {
	def awFile = project(":common").file("src/main/resources/createunlimited.accesswidener")

	forge {
		mixinConfig(
			"createunlimited.mixins.json",
			"createunlimited-common.mixins.json",
		)

		if(awFile.exists()) {
			convertAccessWideners = true
			extraAccessWideners.add loom.accessWidenerPath.get().asFile.name
		}
	}
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon, project.configurations.shade]

    archiveClassifier.set "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set null
}

jar.archiveClassifier.set "dev"

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}
