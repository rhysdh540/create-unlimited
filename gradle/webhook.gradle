import com.google.gson.*

import java.nio.file.Files

buildscript {
	repositories.mavenCentral()
	dependencies {
		classpath 'com.google.code.gson:gson:2.8.9'
	}
}

project.extensions.add('webhook', WebhookExt)

tasks.register('webhook') {
	def config = project.extensions.findByType(WebhookExt)
	if(config == null) {
		throw new IllegalStateException('Webhook extension not found!')
	}
	doLast {
		if(config.url.get() == null || config.url.get().isEmpty()) {
			throw new NullPointerException('Webhook URL not set!')
		}
		def payload = new JsonObject()
		payload.add('content', new JsonPrimitive(config.message.get()))
		send(config.url.get(), config.files.get(), payload)
	}
}

def send(String url, List<File> files, JsonObject payload) {
	def con = new URL(url).openConnection() as HttpURLConnection
	con.requestMethod = 'POST'
	con.doOutput = true
	con.setRequestProperty 'Content-Type', 'multipart/form-data; boundary=boundary'
	con.setRequestProperty 'User-Agent', 'Mozilla/5.0'

	OutputStream os = con.outputStream
	StringBuilder sb = new StringBuilder()

	if (payload != null) {
		sb.append '--boundary\n'
		sb.append 'Content-Disposition: form-data; name="payload_json"\n\n'
		sb.append payload.toString() + '\n'
	}

	if (files != null) {
		for (int i = 0; i < files.size(); i++) {
			File file = files[i]
			sb.append '--boundary\n'
			sb.append "Content-Disposition: form-data; name=\"files[$i]\"; filename=\"${file.name}\"\n"
			sb.append Files.newInputStream(file.toPath()).text + '\n'
		}
	}

	sb.append '\n--boundary--'

	//println sb

	new PrintWriter(new OutputStreamWriter(os, 'UTF-8'), true).withCloseable {
		it.println sb.toString()
	}

	def response = con.responseCode
	def message = con.responseMessage

	if (response == HttpURLConnection.HTTP_OK || response == HttpURLConnection.HTTP_NO_CONTENT) {
		println 'Successfully sent webhook!'
	} else {
		println 'Failed to send webhook!'
		println 'Response code: ' + response
		println 'Message: ' + message
		throw new IOException('Failed to send webhook!')
	}
}

abstract class WebhookExt {

	@Input
	abstract Property<String> url

	@Input
	abstract ListProperty<File> files

	@Input
	abstract Property<String> message

	WebhookExt(Project p) {
		url = p.objects.property(String)
		url.convention('')
		files = p.objects.listProperty(File)
		files.convention([])
		message = p.objects.property(String)
		message.convention('')
	}
}
